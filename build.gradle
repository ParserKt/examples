import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
  repositories { jcenter() }
  dependencies {
    classpath "com.github.jengelman.gradle.plugins:shadow:5.2.0"
  }
  ext {
    pktVersion = "1.1"
  }
}

plugins {
  id "kotlin-multiplatform" version "1.3.61"
}

subprojects {
apply plugin: "kotlin-multiplatform"
apply plugin: "com.github.johnrengelman.shadow"
kotlin {
  jvm(); js()
}

repositories { jcenter() }
ext.kotlin = { name -> return "org.jetbrains.kotlin:kotlin-$name:1.3.61" }

dependencies {
  commonMainImplementation kotlin("stdlib-common")
  jvmMainImplementation kotlin("stdlib")
  jsMainImplementation kotlin("stdlib-js")

  commonTestImplementation kotlin("test-common")
  commonTestImplementation kotlin("test-annotations-common")
  jvmTestImplementation kotlin("test-junit")
  jsTestImplementation kotlin("test-js")
}

repositories {
  maven { url "https://jitpack.io" }
}
dependencies {
  ["", "-util", "-ext"].forEach {
    commonMainImplementation "com.github.parserkt.parserkt:parserkt$it-metadata:$pktVersion"
    jvmMainImplementation "com.github.parserkt.parserkt:parserkt$it-jvm:$pktVersion"
    jsMainImplementation "com.github.parserkt.parserkt:parserkt$it-js:$pktVersion"
  }
}

task shadowJar(type: ShadowJar, dependsOn: [jvmJar]) {
  baseName = project.name.capitalize()
  manifest {}

  from jvmJar
  configurations.add(project.configurations.jvmRuntimeClasspath)
}

build.dependsOn(shadowJar)

} //subprojects

dependencies {
  commonMainImplementation project("hanCalc")
  ["simple", "known", "extended"].forEach { commonMainImplementation project("${it}Syntax") }
}
